/*
 * Copyright (c) Meta Platforms, Inc. and affiliates.
 * All rights reserved.
 *
 * Licensed under the Oculus SDK License Agreement (the "License");
 * you may not use the Oculus SDK except in compliance with the License,
 * which is provided at the time of installation or download, or which
 * otherwise accompanies this software in either electronic or hard copy form.
 *
 * You may obtain a copy of the License at
 * https://developer.oculus.com/licenses/oculussdk/
 *
 * Unless required by applicable law or agreed to in writing, the Oculus SDK
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

plugins {
    id 'com.android.application' version '8.13.2'
}

android {
    compileSdk = 34
    ndkVersion = "29.0.14206865"
    namespace = "com.oculus.xrsamples.xrhands_and_controllers"

    defaultConfig {
        applicationId "com.oculus.xrsamples.xrhands_and_controllers"
        minSdk = 29
        targetSdk = 34
        versionCode = 1
        versionName = "1.0"

        ndk {
            abiFilters = ['arm64-v8a']
        }

        externalNativeBuild {
            cmake {
                arguments = ['-DANDROID_STL=c++_shared']
                cppFlags = ['-std=c++20']
                targets "xrsamples_xrhands_and_controllers"
            }
        }
    }

    sourceSets {
        main {
            manifest.srcFile 'AndroidManifest.xml'
            java.srcDirs = ['../../java']
            assets.srcDirs = ['../../assets']
            res.srcDirs = ['../../res']
        }
    }

    buildTypes {
        debug {
            debuggable = true
            jniDebuggable = true
        }

        release {
            debuggable = false
            minifyEnabled = false
        }
    }

    externalNativeBuild {
        cmake {
            path = file('../../../../CMakeLists.txt')
            version = "3.22.1"
        }
    }

    compileOptions {
        sourceCompatibility = JavaVersion.VERSION_17
        targetCompatibility = JavaVersion.VERSION_17
    }

    buildFeatures {
        prefab = true
    }

    packaging {
        jniLibs {
            useLegacyPackaging = true
        }
    }
}

dependencies {
    // OpenXR loader from Maven Central
    implementation 'org.khronos.openxr:openxr_loader_for_android:1.1.54'
}

// Restart vrshell after uninstall to clear stale icon (zombie icon cleanup)
def execOps = project.services.get(org.gradle.process.ExecOperations)

tasks.matching { it.name.startsWith("uninstall") }.configureEach {
    doLast {
        execOps.exec { spec ->
            spec.commandLine 'adb', 'shell', 'am', 'force-stop', 'com.oculus.vrshell'
        }
        execOps.exec { spec ->
            spec.commandLine 'adb', 'shell', 'monkey', '-p', 'com.oculus.vrshell', '1'
        }
    }
}
